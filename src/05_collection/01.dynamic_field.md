# dynamic_field

## 模块说明

- `dynamic_field(动态字段)`模块定义将结构体和值组合在一起
- 可以使用**任意**名字做字段，也可以在运行时**动态**进行添加和删除
- 名称可以是任何拥有 `copy`、`drop` 和 `store` 能力的值，这些值包括基本类型以及拥有相应能力的结构体
- 任何具有 `store` 能力的值都可以被存储
- 可以不具备`key`能力，即不可直接从外部进行访问

## 源码路径

[dynamic_field.move](https://github.com/MystenLabs/sui/blob/main/crates/sui-framework/packages/sui-framework/sources/dynamic_field.move)

## 方法图解

![](images/dynamic_field.svg)

## 方法说明

| 分类   | 方法                                                                 | 说明                                                                                                       |
| ------ | -------------------------------------------------------------------- | ---------------------------------------------------------------------------------------------------------- |
| **增** | `add<...>(object: &mut UID, name: Name, value: Value)`               | 向对象`object`添加名为`name`的值为`value`的动态字段                                                        |
| **删** | `remove<...>(object: &mut UID, name: Name): Value`                   | 从对象`object`中删除名为`name`的动态字段，若不存在将会报错                                                 |
|        | `remove_if_exists<...>(object: &mut UID, name: Name): Option<Value>` | 从对象`object`中删除名为`name`的动态字段，若存在则已`option::some`包装后返回，若不存在返回`option::none()` |
| **改** | `borrow_mut<...>(object: &mut UID, name: Name): &mut Value`          | 从对象`object`中获取名为`name`的动态字段的可变引用，以便进行对动态字段的修改                               |
| **查** | `borrow<...>(object: &UID, name: Name): &Value`                      | 从对象`object`中获取名为`name`的动态字段的只读引用，用于进行信息查看                                       |
|        | `exists_<...>(object: &UID, name: Name): bool`                       | 若对象`object`中存在名为`name`的动态字段则返回`true`，无需指定`value`类型                                  |
|        | `exists_with_type<...>(object: &UID, name: Name): bool`              | 若对象`object`中存在名为`name`的动态字段则返回`true`，需指定`value`类型                                    |

## 代码示例

采用书架和书本的示例，书本对象作为动态字段添加到书架上。

### 结构定义

```move
// 书架结构定义
public struct Bookshelf has key {
    id: UID,
    // 书本数量
    book_count: u64
}

// 书本结构定义
public struct Book has store {
    // 书本标题
    title: String,
    // 书本描述
    description: String,
}
```

### 创建书架共享对象

```move
public fun create_bookshelf(ctx: &mut TxContext) {
    transfer::share_object(Bookshelf {
        id: object::new(ctx),
        book_count: 0,
    });
}
```

### 添加书本到书架

> 调用`dynamic_field::add`方法。

```move
public fun add_book(bookshelf: &mut Bookshelf, title: vector<u8>, description: vector<u8>) {
    let book = Book {
        title: ascii::string(title),
        description: ascii::string(description)
    };

    dynamic_field::add(&mut bookshelf.id,title, book);
    bookshelf.book_count = bookshelf.book_count + 1;
}
```

### 获取书本

> 调用`dynamic_field::borrow`方法。

```move
public fun get_book(bookshelf: &Bookshelf, title: vector<u8>): &Book {
    dynamic_field::borrow(&bookshelf.id, title)
}
```

### 设置书本的描述信息

> 调用`dynamic_field::borrow_mut`方法。

```move
public fun set_book_desc(bookshelf: &mut Bookshelf, title: vector<u8>, description: vector<u8>) {
    let book_mut_ref: &mut Book = dynamic_field::borrow_mut(&mut bookshelf.id, title);
    book_mut_ref.description = ascii::string(description);
}
```

### 判断书本是否存在

> 调用`dynamic_field::exists_`和`dynamic_field::exists_with_type`方法。

```move
public fun is_book_existed(bookshelf: &Bookshelf, title: vector<u8>): bool {
    dynamic_field::exists_(&bookshelf.id, title)
}

public fun is_book_exists_with_type(bookshelf: &Bookshelf, title: vector<u8>): bool {
    dynamic_field::exists_with_type<vector<u8>, Book>(&bookshelf.id, title)
}
```

### 从书架上移除书本

> 调用`dynamic_field::remove`和`dynamic_field::remove_if_exists`方法。

```move
public fun remove_book(bookshelf: &mut Bookshelf, title: vector<u8>): Book {
    bookshelf.book_count = bookshelf.book_count - 1;
    dynamic_field::remove<vector<u8>, Book>(&mut bookshelf.id, title)
}

public fun remove_if_book_exists(bookshelf: &mut Bookshelf, title: vector<u8>): Option<Book> {
    bookshelf.book_count = bookshelf.book_count - 1;
    dynamic_field::remove_if_exists<vector<u8>, Book>(&mut bookshelf.id, title)
}
```

## 完整代码

- **dynamic_field**

```move
{{#include ./codes/cookbook/sources/dynamic_field.move}}
```

- **dynamic_field_tests**

```move
{{#include ./codes/cookbook/tests/dynamic_field_tests.move}}
```
